import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    dependencies {
        classpath group: 'com.github.rodionmoiseev.gradle.plugins', name: 'idea-utils', version: '0.2'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "xyz.jpenilla.run-paper" version "1.0.6"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'idea-utils'
apply plugin: 'checkstyle'

processResources {
    filter ReplaceTokens, tokens: [
        "apiversion": project.property("apiversion"),
        "version"   : project.property("version")
    ]
}

repositories {
    mavenLocal()
    mavenCentral()

    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
    maven { url = "https://jitpack.io" }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

group = project.property("group")
version = project.property("version")
targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'co.aikar:idb-core:1.0.0-SNAPSHOT'
    implementation 'org.spongepowered:configurate-hocon:4.1.2'
    implementation 'net.kyori:adventure-api:4.10.1'
    implementation 'net.kyori:adventure-platform-bukkit:4.1.0'
    implementation 'net.kyori:adventure-text-serializer-plain:4.10.1'
    implementation 'net.kyori:adventure-text-minimessage:4.10.1'
    implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.3.0'
    implementation group: 'com.cronutils', name: 'cron-utils', version: '9.1.6'

}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

shadowJar {
    relocate 'co.aikar.commands', 'network.darkhelmet.quartz.acf'
    relocate 'co.aikar.locales', 'network.darkhelmet.quartz.locales'
    relocate 'co.aikar.idb', 'network.darkhelmet.quartz.idb'
    relocate 'org.quartz-scheduler', 'network.darkhelmet.quartz-scheduler'
    relocate 'com.cronutils', 'network.darkhelmet.quartz.cronutils'
    relocate 'org.spongepowered', 'network.darkhelmet.quartz.spongepowered'
    relocate 'net.kyori', 'network.darkhelmet.quartz.kyori'

    minimize()
}

runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion("1.18.1")
}